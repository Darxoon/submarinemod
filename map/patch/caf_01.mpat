#define .NpcID:NPC_ExitGoombario 0`

@ $Script_Main {
	Set  *GB_WorldLocation  .Location:GoombaRoad
	Call SetCamPerspective  ( .Cam:Default 00000003 00000019 00000010 00001000 )
	Call SetCamBGColor      ( .Cam:Default 00000000 00000000 00000000 )
	Call SetCamEnabled      ( .Cam:Default .True
	Call SetCamLeadPlayer   ( .Cam:Default .False )
	Exec $Script_MakeEntities
	Exec $Script_SetupMusic
	Exec $Script_EnterMap
	
	Exec $Script_UpdateDoor
	
	Call MakeNpcs ( .False $NpcGroupList )
	
	If *MF_caf_01_SwichHit == .True
		ExecWait $Script_SwitchActivate
	Else
		Bind  $Script_SwitchActivate .Trigger:AreaFlagSet *AreaFlag[.AreaFlag_Entity_Switch1] 00000001 00000000
	EndIf
    
	Return
	End
}

#new:Script $Script_SwitchActivate {
	Call  MakeEntity    ( .Entity:YellowBlock ~Vec4d:Block2 .Item:Mushroom 80000000 )
	Call  AssignBlockFlag   ( *MF_caf_01_Block2Opened )
	Call  MakeEntity    ( .Entity:MultiCoinBrick ~Vec4d:Block3 80000000 )
	Call  AssignBlockFlag   ( *MF_caf_01_Block3Opened )
    
    Set *MF_caf_01_SwichHit 1`
    Unbind
    Return
    End
}

#new:Script $Script_UpdateDoor {
	Loop
		Call GetPlayerPos ( *Var0 *Var1 *Var2 )
		If *Var2 <= -85`
			Call SetGroupEnabled ( ~Model:DoorS .False )
		Else
			Call SetGroupEnabled ( ~Model:DoorS .True )
		EndIf
		Wait 1`
	EndLoop
	Return
	End
}

% Goombario scripting

@ $Script_Callback_EntryE {
	If *MB_Story < .ModProgress:MetGoombario
		Call DisablePlayerInput ( .True )
		Call DisablePartnerAI ( 00000000 )
		Exec $Script_MeetGoombario
	EndIf
	Return
	End
}

#new:Script $Script_MeetGoombario {
	Call SetNpcPos ( .Npc:Partner ~Vec3D:Goombario )
	Wait 60`
	Thread
		Call PlaySound ( 00000262 )
	EndThread
	Call ShowEmote ( .Npc:Partner .Emote:Exclamation -45` 30` .True 0` 0` 0` 0` )
	
	Wait 29`
	Thread
		Call PlayerMoveTo ( ~Vec2D:Mario_MoveTo 30` )
	EndThread
	Wait 1`
	Call NpcMoveTo ( .Npc:Partner ~Vec2D:Goombario_MoveTo 30` )
	
	Call SpeakToPlayer ( .Npc:Partner 00010008 00010001 00000000 002F0001 )
	
	Call EnablePartnerAI ()
	Call DisablePlayerInput ( .False )
	
	Set *MB_Story .ModProgress:MetGoombario
	Return
	End
}

@ $Script_ExitN {
	If *MB_Story < .ModProgress:MetKooper
		ExecWait $Script_ExitToKooper
	EndIf
	
	SetGroup 0000001B
	Call  DisablePlayerInput ( .True )
	Call  UseDoorSounds ( .DoorSounds:Basic )
	Set   *Var[0]  ~Entry:entry_n
	Set   *Var[1]  ~Collider:door_n
	Set   *Var[2]  ~Model:door3
	Set   *Var[3]  .DoorSwing:Out
	Exec  ExitSingleDoor
	Wait  17`
	Call  GotoMap   ( "caf_02" ~Entry:caf_02:Entry0 )
	Wait  100`
	Return
	End
}

#new:Script $Script_ExitToKooper {
	Call DisablePlayerInput ( .True )
	Call DisablePartnerAI ( 00000000 )
	
	Call GetNpcPos ( .Npc:Partner *VarA *VarB *VarC )
	Call SetNpcPos ( .Npc:Partner 0` -300` 0` )
	Call SetNpcPos ( .NpcID:NPC_ExitGoombario *VarA *VarB *VarC )
	Call SpeakToPlayer ( .NpcID:NPC_ExitGoombario 00010008 00010001 00000000 002F0002 )
	
	Call DisablePlayerInput ( .False )
	
	Call $Function_DisableAllPartners (  )
	Call $Function_EnableKooper (  )
	Return
	End
}

% NPC Goombario
#new:NpcGroupList $NpcGroupList {
	00000001 $NpcGroup_Goombario      00000000
	00000000 00000000 00000000
}

#new:NpcGroup $NpcGroup_Goombario {
	.NpcID:NPC_ExitGoombario $NpcSettings_Goombario ~Vec3f:NPC_ExitGoombario % -50 0 -30
	00002809 $Script_Init_Goombario 00000000 00000000 00000000
	~NoDrops
	~Movement:NPC_ExitGoombario
	~AnimationTable:NPC_ExitGoombario % .Sprite:WorldGoombario
	00000000 00000000 00000000 00000000 % no tattle string
}

#new:NpcSettings $NpcSettings_Goombario
{
	00000000 00160018 00000000 00000000 00000000 00000000 00000000 00000000
	00000000 00000000 00630010
}

#new:Script $Script_Init_Goombario {
	Return
	End
}
