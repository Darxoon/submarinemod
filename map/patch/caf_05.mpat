#define .NpcID:Kooper 0`
#define .NpcID:Goombario 1`
#define .NpcID:Watt 2`

@ $Header {
	[MainScript] $Script_Main
	[Background] 80200000
	[EntryList] $EntryList
	[EntryCount] 6
	[MapTattle] 002F0017
}

@ $Script_Main {
	Set  *GB_WorldLocation  .Location:GoombaRoad
	Call SetCamPerspective  ( .Cam:Default 00000003 00000019 00000010 00001000 )
	Call SetCamBGColor      ( .Cam:Default 00000000 00000000 00000000 )
	Call SetCamEnabled      ( .Cam:Default .True
	Call SetCamLeadPlayer   ( .Cam:Default .False )
	Exec $Script_MakeEntities
	Exec $Script_SetupMusic
	Exec $Script_EnterMap
	
	Exec $aaa
	
	Call MakeNpcs ( .False $NpcGroupList )
	Return
	End
}

#new:Script $aaa {
	Loop
		Call GetPlayerTargetYaw ( *Debug[0] )
		Wait 1`
	EndLoop
	Return
	End
}

@ $Script_ExitN {
    If *MF_caf_02_Door2Unlocked == .False
        Call DisablePlayerInput ( .True )
        Call  ShowMessageAtScreenPos    ( 001D016A  160`  40` ) % It won't open. It must be locked from the inside.
		Call DisablePlayerInput ( .False )
        Return
    EndIf
    
	SetGroup 0000001B
	Call  DisablePlayerInput ( .True )
	Call  UseDoorSounds ( .DoorSounds:Metal )
	Set   *Var[0]  ~Entry:entry_n
	Set   *Var[1]  ~Collider:door_n
	Set   *Var[2]  ~Model:door3
	Set   *Var[3]  .DoorSwing:Out
	Exec  ExitSingleDoor
	Wait  17`
	Call  GotoMap   ( "caf_02" ~Entry:caf_02:Entry1 )
	Wait  100`
	Return
	End
}

@ $Script_Callback_WalkW {
	If *MB_Story >= .ModProgress:MetKooper
		If *MF_caf_01_Key2Obtained == .False
			If *MF_caf_05_StorageRoomHintShown == .False
				Call DisablePlayerInput ( .True )
				Call DisablePartnerAI ( 00000000 )
				Set *MB_Debug 8
				Wait 30`
				Set *MB_Debug 9
				Exec $Script_StorageRoomHint
			EndIf
		EndIf
	EndIf
	Return
	End
}

@ $Script_Callback_DoorN {
	If *MB_Story < .ModProgress:MetKooper
		Return
	EndIf
	If *MF_caf_01_Key2Obtained == .True
		Return
	EndIf
	If *MF_caf_05_StorageRoomHintShown == .True
		Return
	EndIf
	
	Loop
		Call GetPlayerPos ( *Var0 *Var1 *Var2 )
		If *Var2 >= -60`
			If *Var0 < -120`
				BreakLoop
			EndIf
		EndIf
		Wait 1`
	EndLoop
	
	Call DisablePlayerInput ( .True )
	Call DisablePartnerAI ( 00000000 )
	Exec $Script_StorageRoomHint
	Return
	End
}

#new:Script $Script_StorageRoomHint {
	Set *MF_caf_05_StorageRoomHintShown .True
	
	Thread
		Call PlaySound ( 00000262 )
	EndThread
	Call ShowEmote ( .Npc:Partner .Emote:Exclamation -45` 30` .True 0` 0` 0` 0` )
	
	Wait 10`
	
	Thread
		Wait 10`
		Call PlayerMoveTo ( ~Vec2D:Player_MoveTo 40` )
		Call PlayerFaceNpc ( .Npc:Partner .False )
	EndThread
	
	Call GetCurrentPartnerID ( *Var0 )
	Switch *Var0
		Case == .Partner:Goombario
			Call SetNpcAnimation ( .Npc:Partner 00010003 )
			Call NpcMoveTo ( .Npc:Partner ~Vec2D:Partner_MoveTo 40` )
			Call SetNpcAnimation ( .Npc:Partner 00010001 )
			
			Wait 10`
			
			Call SpeakToPlayer ( .Npc:Partner 00010008 00010001 00000000 002F000C )
		Case == .Partner:Kooper
			Call SetNpcAnimation ( .Npc:Partner 00020005 )
			Call NpcMoveTo ( .Npc:Partner ~Vec2D:Partner_MoveTo 40` )
			Call SetNpcAnimation ( .Npc:Partner 00020003 )
			
			Wait 10`
			
			Call SpeakToPlayer ( .Npc:Partner 0002000D 00020003 00000000 002F000B )
	EndSwitch
	
	Call EnablePartnerAI ()
	Call DisablePlayerInput ( .False )
	Return
	End
}

@ $Script_ExitS {
	If *MB_Story < .ModProgress:MetBow
		Declare *GoombarioOut
		
		Call GetCurrentPartnerID ( *Var0 )
		If *Var0 == .Partner:Goombario
			Call SetNpcAnimation ( .Npc:Partner 00010003 )
			Call NpcMoveTo ( .Npc:Partner ~Vec2D:Kooper_MoveTo 30` )
			Call SetNpcAnimation ( .Npc:Partner 00010001 )
			
			Call BringPartnerOut ( .Partner:Kooper )
			Set *GoombarioOut .True
		Else
			Set *GoombarioOut .False
		EndIf
		
		Call DisablePlayerInput ( .True )
		Call DisablePartnerAI ( 0000000 )
		
		Thread
			Call GetPlayerPos ( *Var0 *Var1 *Var2 )
			Call PlayerMoveTo ( -224` *Var2 15` )
			Call InterpPlayerYaw ( 000000A0 0` )
		EndThread
		
		Call SetNpcAnimation ( .Npc:Partner 00020005 )
		Call NpcMoveTo ( .Npc:Partner ~Vec2D:Kooper_MoveTo 30` )
		Call SetNpcAnimation ( .Npc:Partner 00020003 )
		
		Call NpcFacePlayer ( .Npc:Partner 00000000 )
		Wait 8`
		
		Call SpeakToPlayer ( .Npc:Partner 0002000D 00020003 00000000 002F000D )
		
		If *MF_caf_01_GoombarioObtainedPermanently == .True
			If *GoombarioOut == .False
				Call BringPartnerOut ( .Partner:Goombario )
				Call SpeakToPlayer ( .Npc:Partner 00010008 00010001 00000000 002F000E )
			Else
				Call SpeakToPlayer ( -5` 00010008 00010001 00000000 002F000E )
			EndIf
		EndIf
		
		Call $Function_DisableAllPartners ()
		Call $Function_EnableBow ()
	EndIf
	
	SetGroup 0000001B
	Call  DisablePlayerInput ( .True )
	Call  UseDoorSounds ( .DoorSounds:Metal )
	Set   *Var[0]  ~Entry:entry_s
	Set   *Var[1]  ~Collider:door_s
	Set   *Var[2]  ~Model:door
	Set   *Var[3]  .DoorSwing:In
	Exec  ExitSingleDoor
	Wait  17`
	Call  GotoMap   ( "caf_04" ~Entry:caf_04:Entry0 )
	Wait  100`
	Return
	End
}

% Kooper & Goombario
#new:NpcGroupList $NpcGroupList {
	00000001 $NpcGroup_Kooper      00000000
	00000001 $NpcGroup_Goombario      00000000
	00000001 $NpcGroup_Watt      00000000
	00000000 00000000 00000000
}

#new:NpcGroup $NpcGroup_Kooper {
	.NpcID:Kooper $NpcSettings_Kooper ~Vec3f:NPC_Kooper % 20 0 -145
	00400D09 $Script_Init_Kooper 00000000 00000000 00000000
	~NoDrops
	~Movement:NPC_Kooper
	~AnimationTable:NPC_Kooper
	00000000 00000000 00000000 00000000
}

#new:NpcSettings $NpcSettings_Kooper {
	00000000 00230018 00000000 00000000 00000000 00000000 00000000 00000000
	00000000 00000000 00630010
}

#new:Script $Script_Init_Kooper {
	If *MB_Story < .ModProgress:MetBow
		Call RemoveNpc ( .Npc:Self )
		Return
	EndIf
	If *MF_caf_05_PartnersRegained == .True
		Call RemoveNpc ( .Npc:Self )
		Return
	EndIf
	Call BindNpcInteract ( .Npc:Self $Script_Interact_Kooper )
	Return
	End
}

#new:Script $Script_Interact_Kooper {
	Call DisablePlayerInput ( .True )
	Call PlayerMoveTo ( ~Vec2D:Mario_MoveTo_Kooper 20` )
	Call SpeakToPlayer ( .Npc:Self 00010008 00010001 00000000 002F0012 )
	
	If *MF_caf_01_GoombarioObtainedPermanently == .True
		Call $Function_EnableGoombarioInBg (  )
	EndIf
	Call $Function_EnableKooperInBg (  )
	Set *MF_caf_05_PartnersRegained .True
	
	Call GotoMap ( "caf_05" ~Entry:Mario_MoveTo_Kooper )
	Return
	End
}

#new:NpcGroup $NpcGroup_Goombario {
	.NpcID:Goombario $NpcSettings_Goombario ~Vec3f:NPC_Goombario % -50 0 -30
	00002809 $Script_Init_Goombario 00000000 00000000 00000000
	~NoDrops
	~Movement:NPC_Goombario
	~AnimationTable:NPC_Goombario % .Sprite:WorldGoombario
	00000000 00000000 00000000 00000000 % no tattle string
}

#new:NpcSettings $NpcSettings_Goombario
{
	00000000 00160018 00000000 00000000 00000000 00000000 00000000 00000000
	00000000 00000000 00630010
}

#new:Script $Script_Init_Goombario {
	If *MB_Story < .ModProgress:MetBow
		Call RemoveNpc ( .Npc:Self )
		Return
	EndIf
	If *MF_caf_01_GoombarioObtainedPermanently == .False
		Call RemoveNpc ( .Npc:Self )
		Return
	EndIf
	If *MF_caf_05_PartnersRegained == .True
		Call RemoveNpc ( .Npc:Self )
		Return
	EndIf
	Call BindNpcInteract ( .Npc:Self $Script_Interact_Goombario )
	Return
	End
}

#new:Script $Script_Interact_Goombario {
	Call DisablePlayerInput ( .True )
	Call PlayerMoveTo ( ~Vec2D:Mario_MoveTo_Goombario 20` )
	Call SpeakToPlayer ( .Npc:Self 00010008 00010001 00000000 002F0012 )
	
	Call $Function_EnableGoombarioInBg (  )
	Call $Function_EnableKooperInBg (  )
	Set *MF_caf_05_PartnersRegained .True
	
	Call GotoMap ( "caf_05" ~Entry:Mario_MoveTo_Goombario )
	Return
	End
}

#new:NpcGroup $NpcGroup_Watt
{
	.NpcID:Watt $NpcSettings_Watt ~Vec3f:NPC_Watt % 0 -1000 0
	00000D09 $Script_Init_Watt 00000001 00000000 0000010E
	~NoDrops
	~Movement:NPC_Watt
	~AnimationTable:NPC_Watt % .Sprite:WorldWatt
	00000000 00000000 00000000 00000000 % Say... This is the lantern that belongs to the Big ...
}

#new:NpcSettings $NpcSettings_Watt
{
	00000000 00170016 00000000 00000000 00000000 80077F70 00000000 8007809C
	00000000 00000000 000E0001
}

#new:Script $Script_Init_Watt {
	If *MF_caf_05_FloorDestroyed == .True
		Call RemoveNpc ( .Npc:Self )
	EndIf
	Return
	End
}

@ $Script_BreakFloor_BoardedFloor {
	Set  *MF_caf_05_FloorDestroyed  .True
	Call SetNpcPos ( .NpcID:Watt ~Vec3D:Watt_Position )
	Exec $Script_MeetWatt
	
	Return
	End
}

#new:Script $Script_MeetWatt {
	Call DisablePlayerInput ( .True )
	
	Wait 30`
	Call SetNpcAnimation ( .NpcID:Watt 00060006 )
	Call SetNpcFlagBits ( .NpcID:Watt 00000100 .True ) % ignore player collision
	
	Call GetNpcPos ( .NpcID:Watt *Var0 *Var1 *Var2 )
	Add *Var1 30`
	Call NpcFlyTo ( .NpcID:Watt *Var0 *Var1 *Var2 20` 0` .Easing:Linear )
	
	Wait 19`
	Call SetNpcAnimation ( .NpcID:Watt 00060001 )
	Wait 1`
	Call SpeakToPlayer ( .NpcID:Watt 00060004 00060001 00000000 002F0014 )
	Call $Function_EnableWatt (  )
	Call GotoMap ( "caf_05" ~Entry:EntryWatt )
	Return
	End
}